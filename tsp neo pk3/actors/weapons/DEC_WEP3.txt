//===========================================================================
//
// Blackwood Arms 240 "Zeke" Shotgun
//
// by Marty Kirra
// Sprites by Scuba Steve, Snarboo, and Marty Kirra
//
//===========================================================================

ACTOR ZekeShotgun : Weapon Replaces Shotgun 
{
	Weapon.Kickback 400
	Weapon.AmmoType	"ShotgunMag"
	Weapon.AmmoGive	0
	Weapon.AmmoUse	1
	Weapon.AmmoType2 "TSPShells"
	Weapon.AmmoGive2 8
	Weapon.AmmoUse2	0
	Inventory.PickupMessage "[Blackwood Arms 240 Zeke] [Class: Shotgun, Slot 3]"
	Decal BulletChip
	Obituary "%o went head to head with %k's shotgun. It went as well as you'd expect."
	+NOAUTOFIRE
	+Weapon.AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.NoAlert
	Tag "Blackwood Arms 240 Zeke"
   States
   {
   Spawn:
	  STGP A -1
	  Stop
   Ready:
      TNT1 A 0 A_TakeInventory("ShotgunLoading", 99)
      STGG A 1 A_WeaponReady
	  TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
      LOOP
   Deselect:
      TNT1 A 0 A_PlaySound("zekeshotgun/down", 1)
	  STGD ABCDE 1
	  TNT1 A 3 
   DeselectLoop:
      TNT1 A 1 A_Lower
      Loop
   Select:
      TNT1 A 0 A_JumpIfInventory("ComboCooldown", 1, "QuickSelect")
      TNT1 A 3 A_PlaySound("zekeshotgun/up", 5)
	  STGU ABCD 1
	  STGR DCBA 1
	  STGC D 1
	  STGG A 2
   SelectLoop:	  
	  STGG A 1 A_Raise
      Loop
   QuickSelect:
      TNT1 A 0 A_TakeInventory("ComboCooldown", 99)
      TNT1 A 1 A_PlaySound("zekeshotgun/up", 5)
	  STGU IHGFE 1
	  Goto SelectLoop
   Fire:
      TNT1 A 0 A_JumpIfInventory("ShotgunLoading", 0, "ReloadFinish")
      STGG A 0 A_JumpIfInventory("ShotgunFired", 1, "Pump")
	  TNT1 A 0 A_JumpIfNoAmmo("Empty")
	  STGG A 0 A_AlertMonsters
	  STGG A 0 A_GunFlash
	  STGG A 0 A_PlaySound("zekeshotgun/fire", 1)
	  STGF A 0 A_Light2
      STGF A 1 BRIGHT A_FireBullets(7,0,8,5,"BulletPuff",1)
	  STGF B 1 BRIGHT A_Light1
	  STGF A 0 A_SetPitch(pitch - 1.0)
	  STGG B 1 A_Light0
	  STGG CDEEDCB 1 
	  STGG A 0 A_GiveInventory("ShotgunFired", 1)
	  STGG A 1 A_WeaponReady
	  STGG A 1 A_WeaponReady
	  Goto Ready
	Pump:
	  STGG A 0 A_Jump(125,"Pump2")
	  STGG A 0 A_TakeInventory("ShotgunFired",1)
	  STGG A 0 A_PlaySound("zekeshotgun/pump", 5)
	  STGC AABBCCCBBAA 1
	  STGG A 1
	  Goto Ready
    Pump2:
	  STGG A 0 A_TakeInventory("ShotgunFired",1)
	  STGG A 0 A_PlaySound("zekeshotgun/pump", 5)
	  STGC DDEFGGHIJJIHGGFED 1
	  Goto Ready
    Empty:
	  STGG A 5 A_PlaySound("919m/dry", 5)
	  Goto Reload
	Reload:
      STGG A 0 A_JumpIfInventory("ShotgunMag",8,2)
	  STGG A 0 A_JumpIfInventory("TSPShells",1,2)
	  STGG A 1 A_WeaponReady
	  Goto Ready
	ReloadStart:
      STGC D 1
	  STGR ABCD 1 
	ReloadWork:
	  TNT1 A 0 A_GiveInventory("ShotgunLoading", 1)
	  STGR D 5 A_WeaponReady
	  STGR D 1 A_PlaySound("zekeshotgun/load", 5)
	  STGR D 0 A_TakeInventory("TSPShells",1)
	  STGR D 0 A_GiveInventory("ShotgunMag",1)
	  STGR EFGHHIJ 1
	  STGR K 1 A_WeaponReady
      STGR K 0 A_JumpIfInventory("ShotgunMag",8,"ReloadFinish")
	  STGR K 0 A_JumpIfInventory("TSPShells",1,"ReloadWork")
	ReloadFinish:
	  STGR DCBA 1
	  STGC D 1
	  STGG A 3 A_JumpIfInventory("ShotgunFired", 0, "ReloadPump")
	  STGG A 0 A_TakeInventory("ShotgunLoading", 99)
	  STGG A 0 A_WeaponReady
	  Goto Ready
	ReloadPump: 
	  STGG A 0 A_PlaySound("zekeshotgun/pump", 5)
	  STGG A 0 A_TakeInventory("ShotgunFired",1)
	  STGC AABBCCCBBAA 1
	  STGG A 2 A_WeaponReady
	  Goto Ready
   }
}

ACTOR ShotgunMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 8
}

ACTOR ShotgunLoading : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR ShotgunFired : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR ShotgunPump : Inventory
{
	Inventory.MaxAmount 1
}


ACTOR TSPShells : Ammo replaces Shell
{
	Inventory.Amount 4
	Inventory.MaxAmount 48
	Ammo.BackpackAmount 16
	Ammo.BackpackMaxAmount 96
	Ammo.DropAmount 4
	Inventory.Icon "BCKSA0"
	Inventory.PickupMessage "Picked up buckshot shells. [+4 Shotgun Shells]"
	Inventory.PickupSound "zekeshotgun/load"
	States
	{
	Spawn:
        BCKS A -1
		Stop
	}
}

ACTOR ShellBoxAmmoSpawner replaces ShellBox
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("TSPShells", 1,0)
		TNT1 A 0 A_SpawnItem("TSPShells", 1,0)
		TNT1 A 0 A_SpawnItem("TSPShells", 1,0)
		Stop
	}
}

ACTOR ShotgunPickup : CustomInventory
{
    Inventory.PickupMessage "[Blackwood Arms 240 Zeke] [Class: Shotgun, Slot 3]"
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
      STGP A -1
	  Stop
	Pickup:
	  TNT1 A 0 A_JumpIfInventory("ZekeShotgun", 1, "ShellCheck")
	  TNT1 A 0 A_GiveInventory("FlavorPickupZekeShotgun", 1)
	  TNT1 A 0 A_GiveInventory("ZekeShotgun", 1)
	  TNT1 A 0 A_GiveInventory("TSPShells", 8)
	ShellCheck:
      TNT1 A 0 A_JumpIfInventory("TSPShells", 0, "PickupFail")
	  TNT1 A 0 A_GiveInventory("TSPShells", 8)
	  Stop
	PickupFail:
      STGP A 1
      Fail
	}
}
